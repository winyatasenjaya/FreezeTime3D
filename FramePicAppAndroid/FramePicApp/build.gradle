import groovy.json.JsonSlurper
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:2.5.0'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        def jsonFile = new File("../Server/socketMessages.json")
        def jsonMessages = new JsonSlurper().parseText(jsonFile.text)

        jsonMessages.picTakerMessages.each {
            key, val -> buildConfigField("String", "pic_" + key, "\"" + val + "\"")
        }

        jsonMessages.masterMessages.each {
            key, val -> buildConfigField("String", "master_" + key, "\"" + val + "\"")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //https://github.com/davidgatti/dgCam
    //'me.tatarka:gradle-retrolambda:2.5.0'
    compile 'com.netflix.rxjava:rxjava-android:0.20.5'
    compile 'com.koushikdutta.async:androidasync:1.1.6'
    compile 'com.jakewharton:butterknife:6.0.0'
}
